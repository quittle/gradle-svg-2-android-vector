buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.1.3.201810200350-r'
    }
}

import org.eclipse.jgit.api.Git

plugins {
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'findbugs'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    jcenter()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget("HEAD")
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        svg2androidVector {
            id = 'com.quittle.svg-2-android-vector'
            implementationClass = 'com.quittle.svg2androidvector.Svg2AndroidVectorPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'svg-2-android-vector'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-svg-2-android-vector'
    vcsUrl = 'https://github.com/quittle/gradle-svg-2-android-vector'
    description = 'Automatically converts SVGs to Android Vector files as part of the build.'
    tags = ['android', 'android vector', 'svg', 'android resources']

    plugins {
        svg2androidVector {
            displayName = 'Automated SVG to Android Vector converter'
        }
    }
}

dependencies {
    compile 'com.android.tools.build:gradle:2.3.3'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352
}

findbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
