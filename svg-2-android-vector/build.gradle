buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.6.1.202002131546-r'
    }
}

import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '4.0.0'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    jcenter()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget('HEAD')
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        svg2androidVector {
            id = 'com.quittle.svg-2-android-vector'
            implementationClass = 'com.quittle.svg2androidvector.Svg2AndroidVectorPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'svg-2-android-vector'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-svg-2-android-vector'
    vcsUrl = 'https://github.com/quittle/gradle-svg-2-android-vector'
    description = 'Automatically converts SVGs to Android Vector files as part of the build.'
    tags = ['android', 'android vector', 'svg', 'android resources']

    plugins {
        svg2androidVector {
            displayName = 'Automated SVG to Android Vector converter'
        }
    }
}

compileJava {
    options.compilerArgs << '-Xlint:all' << '-Werror' << '-proc:none'
}

dependencies {
    annotationProcessor 'com.google.code.findbugs:annotations:3.0.1'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:3.5.3'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352
}

spotbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
