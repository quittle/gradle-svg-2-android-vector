buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:6.6.0.202305301015-r'
    }
}

import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '5.1.3'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    mavenCentral()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget('HEAD')
        .setTags(true)
        .call()

final String PLUGIN_ID = System.getenv('TEST_PLUGIN_ID_OVERRIDE') ?: 'svg-2-android-vector'

gradlePlugin {
    plugins {
        svg2androidVector {
            id = 'com.quittle.svg-2-android-vector'
            implementationClass = 'com.quittle.svg2androidvector.Svg2AndroidVectorPlugin'

            displayName = 'Automated SVG to Android Vector converter'
            description = 'Automatically converts SVGs to Android Vector files as part of the build.'
            tags.set(['android', 'android vector', 'svg', 'android resources'])
            website = 'https://github.com/quittle/gradle-svg-2-android-vector'
            vcsUrl = 'https://github.com/quittle/gradle-svg-2-android-vector'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = PLUGIN_ID
        }
    }
}

compileJava {
    options.compilerArgs << '-Xlint:all' << '-Werror' << '-proc:none'
}

dependencies {
    annotationProcessor group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.3'
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.3'

    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:8.0.2' // TODO: remove?
    implementation 'com.android.tools:sdk-common:31.0.2'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352
}

spotbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
